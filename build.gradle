plugins {
    id 'base'
    id 'java-library'
    id 'maven-publish'
    id "com.github.node-gradle.node" version "7.0.2"
}

group = 'uk.badamson.mc'
version = '3.3.3-SNAPSHOT'
description = 'MC-front-end'

def releasesRepoUrl = 'http://thegn:8081/releases/'
def snapshotsRepoUrl = 'http://thegn:8081/snapshots/'

repositories {
    mavenLocal()
    maven {
        url releasesRepoUrl
        allowInsecureProtocol = true
    }
    maven {
        url snapshotsRepoUrl
        allowInsecureProtocol = true
    }
    mavenCentral()
}

node {
    version = "v16.15.0"
    npmVersion = "8.5.5"
    nodeProjectDir = file("${project.projectDir}/src/main/web/mc-front-end")
}

tasks.npm_install {
}
tasks.npm_run_build {
    inputs.files fileTree("${project.projectDir}/src/main/web/mc-front-end")
    outputs.dir "${project.projectDir}/target"
}


tasks.register('packageNpmApp', Zip) {
    dependsOn npm_run_build
    archiveBaseName = 'mc-font-end'
    archiveExtension = 'jar'
    destinationDirectory = file("${projectDir}/build/npm")
}

String testsExecutedMarkerName = "${projectDir}/.tests.executed"
tasks.register('testNpm', NpmTask) {
    dependsOn assemble
    args = ['run', 'test-headless']

    inputs.files fileTree("${project.projectDir}/src/main/web/mc-front-end")

    // allows easy triggering re-tests
    doLast {
        new File(testsExecutedMarkerName).text = 'delete this file to force re-execution JavaScript tests'
    }
    outputs.file testsExecutedMarkerName
}

assemble.dependsOn packageNpmApp
check.dependsOn testNpm

configurations {
    npmResources
}

configurations.default.extendsFrom(configurations.npmResources)

artifacts {
    npmResources(packageNpmApp.archivePath) {
        builtBy packageNpmApp
        type "jar"
    }
}

java.sourceCompatibility = JavaVersion.VERSION_17

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
